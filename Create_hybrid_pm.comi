/CREATE HYBRID PM FOR THE OPTIMIZATION
/21/05/2012

/RESET HISTORY
/UNDO STATE=ROOT |  HISTORY STATES=0 


/ Read Parameters from external data file
/ ============================================
$ COMINPUT FILE=dimensions_hybrid_pm_constant.comi MODE=CONT COMMENTSTYLE=LINE
$ COMINPUT FILE=dimensions_hybrid_pm_variable.comi MODE=CONT COMMENTSTYLE=LINE

/SET VIEW
/THREED XORIGIN=50 YORIGIN=30 ZORIGIN=0 ROTX=0 ROTY=0 ROTZ=0 SIZE=25 PERSPECTIVE=YES LINECOLOUR=YES XASPECT=1 YASPECT=1 ZASPECT=1 OPTION=SETVIEW 

/CREATE BACKGROUND AIR
SKETCH OBJECT=WIREEDGE +COMPLETE
WIREEDGE  CONTINUE=AUTOCOVER  Name='air' X0=0 Y0=0 Z0=0 X1=#R_out Y1=0 Z1=0 EDGETYPE=STRAIGHT
WIREEDGE CONTINUE=AUTOCOVER  X1=#R_out*cosd(#alfa_sym) Y1=#R_out*sind(#alfa_sym) Z1=0 EDGETYPE=CENTRE X2=0 Y2=0 Z2=0 
WIREEDGE  CONTINUE=AUTOCOVER  X1=0 Y1=0 Z1=0 EDGETYPE=STRAIGHT

/Sweep Airgap
PICK OPTION=ADD PROPERTY=UniqueName  LABEL='air'
PICK OPTION=CHANGE TYPE=FACE
SWEEP KEEP=NO TYPE=DISTANCE REGULAR=NO RIGID=NO DISTANCE=#w DRAFTTYPE=NONE

/DEFINE BOUNDARY
PICK OPTION=TOGGLE TYPE=FACE UNIQUEBODYNAME='air' IDENTIFIER=E.00001
PICK OPTION=TOGGLE TYPE=FACE UNIQUEBODYNAME='air' IDENTIFIER=D.00001
PICK OPTION=TOGGLE TYPE=FACE UNIQUEBODYNAME='air' IDENTIFIER=E.00002
PICK OPTION=TOGGLE TYPE=FACE UNIQUEBODYNAME='air' IDENTIFIER=E.00004
FACEDATA OPTION=MODIFY BOUNDARYLABEL='tangent_mg' ELEMENTTYPE=Linear FORMETHOD=NONE BACKMETHOD=NONE

PICK OPTION=TOGGLE TYPE=FACE UNIQUEBODYNAME='air' IDENTIFIER=E.00003
FACEDATA OPTION=MODIFY BOUNDARYLABEL='normal_mg' ELEMENTTYPE=Linear FORMETHOD=NONE BACKMETHOD=NONE

/boundary types
BOUNDARY GUIINIT
BOUNDARY PICK 'normal_mg'
BOUNDARY OPTION=MODIFY CONDITION=NORMMAGN 
BOUNDARY UNPICK 'normal_mg' | BOUNDARY PICK 'tangent_mg'
BOUNDARY OPTION=MODIFY CONDITION=TANGMAGN 

/DRAW STATOR
SKETCH OBJECT=WIREEDGE +COMPLETE
WIREEDGE  CONTINUE=AUTOCOVER  Name='stator' X0=#R_out-#hs Y0=0 Z0=0 X1=#R_out Y1=0 Z1=0 EDGETYPE=STRAIGHT
WIREEDGE CONTINUE=AUTOCOVER  X1=#R_out*cosd(#alfa_sym) Y1=#R_out*sind(#alfa_sym) Z1=0 EDGETYPE=CENTRE X2=0 Y2=0 Z2=0 
WIREEDGE  CONTINUE=AUTOCOVER  X1=(#R_out-#hs)*cosd(#alfa_sym) Y1=(#R_out-#hs)*sind(#alfa_sym) Z1=0 EDGETYPE=STRAIGHT
WIREEDGE CONTINUE=AUTOCOVER  X1=#R_out-#hs Y1=0 Z1=0 EDGETYPE=CENTRE X2=0 Y2=0 Z2=0 

/Sweep Stator
PICK OPTION=ADD PROPERTY=UniqueName  LABEL='stator'
PICK OPTION=CHANGE TYPE=FACE
SWEEP KEEP=NO TYPE=DISTANCE REGULAR=NO RIGID=NO DISTANCE=#w DRAFTTYPE=NONE

/Stator Properties
PICK OPTION=ADD PROPERTY=UniqueName LABEL='stator'
CELLDATA OPTION=MODIFY MATERIALLABEL='Steel' POTENTIAL=Default ELEMENTTYPE=Linear LEVEL=5 SIZE=5 ELEMSHAPEPREF=NONE

/DRAW ROTOR
SKETCH OBJECT=WIREEDGE +COMPLETE
WIREEDGE  CONTINUE=AUTOCOVER  Name='rotor' X0=0 Y0=0 Z0=0 X1=#R_out-#hs-#ac Y1=0 Z1=0 EDGETYPE=STRAIGHT
WIREEDGE CONTINUE=AUTOCOVER  X1=#R_in*cosd(#alfa_sym) Y1=#R_in*sind(#alfa_sym) Z1=0 EDGETYPE=CENTRE X2=0 Y2=0 Z2=0 
WIREEDGE  CONTINUE=AUTOCOVER  X1=0 Y1=0 Z1=0 EDGETYPE=STRAIGHT

/Sweep Rotor
PICK OPTION=ADD PROPERTY=UniqueName  LABEL='rotor'
PICK OPTION=CHANGE TYPE=FACE
SWEEP KEEP=NO TYPE=DISTANCE REGULAR=NO RIGID=NO DISTANCE=#w DRAFTTYPE=NONE

/Rotor Properties
PICK OPTION=ADD PROPERTY=UniqueName LABEL='rotor'
CELLDATA OPTION=MODIFY MATERIALLABEL='Steel' POTENTIAL=Default ELEMENTTYPE=Linear LEVEL=5 SIZE=5 ELEMSHAPEPREF=NONE

/DRAW NdFeB MAGNET
/dimensions as a ratio of R_in and alfa_sym
/Rectangular Magnet
/BLOCK Name='Neo' X0=#k_radius_neo*#R_in*cosd(#k_angle_neo*#alfa_sym) Y0=#k_radius_neo*#R_in*sind(#k_angle_neo*#alfa_sym) Z0=0 X1=#k_radius_neo*#R_in*cosd(#k_angle_neo*#alfa_sym)-#h_neo Y1=0 Z1=#w MATERIALLABEL='NdFeB' LEVEL=10

/Smooth Rectangle Magnet
SKETCH OBJECT=WIREEDGE +COMPLETE
WIREEDGE  CONTINUE=AUTOCOVER  Name='Neo' X0=#k_radius_neo*#R_in Y0=0 Z0=0 X1=#k_radius_neo*#R_in*cosd(#k_angle_neo*#alfa_sym) Y1=#k_radius_neo*#R_in*sind(#k_angle_neo*#alfa_sym) Z1=0 EDGETYPE=CENTRE X2=0 Y2=0 Z2=0 
WIREEDGE  CONTINUE=AUTOCOVER  X1=#k_radius_neo*#R_in*cosd(#k_angle_neo*#alfa_sym)-#h_neo Y1=#k_radius_neo*#R_in*sind(#k_angle_neo*#alfa_sym) Z1=0 EDGETYPE=STRAIGHT
WIREEDGE  CONTINUE=AUTOCOVER  X1=#k_radius_neo*#R_in*cosd(#k_angle_neo*#alfa_sym)-#h_neo Y1=0 Z1=0 EDGETYPE=STRAIGHT
WIREEDGE CONTINUE=AUTOCOVER  X1=#k_radius_neo*#R_in Y1=0 Z1=0 EDGETYPE=STRAIGHT

/Sweep Neo
PICK OPTION=ADD PROPERTY=UniqueName LABEL='Neo'
PICK OPTION=CHANGE TYPE=FACE
SWEEP KEEP=NO TYPE=DISTANCE REGULAR=NO RIGID=NO DISTANCE=#w DRAFTTYPE=NONE

PICK OPTION=ADD PROPERTY=UniqueName LABEL='Neo'
CELLDATA OPTION=MODIFY MATERIALLABEL='NdFeB' POTENTIAL=Default ELEMENTTYPE=Linear LEVEL=10 VOLUMELABEL='Neo_orientation' SIZE=5 ELEMSHAPEPREF=NONE

/DRAW EXTRA AIR (for NdFeB)
#R_air=(1-#k_extra_air)*sqrt((#h_neo*sind(#k_angle_neo*#alfa_sym))**2+(#R_in-#h_neo*cosd(#k_angle_neo*#alfa_sym))**2)+#k_extra_air*#R_in

SKETCH OBJECT=WIREEDGE +COMPLETE
WIREEDGE  CONTINUE=AUTOCOVER  Name='extra_air' X0=#R_air Y0=0 Z0=0 X1=#R_in Y1=0 Z1=0 EDGETYPE=STRAIGHT
WIREEDGE CONTINUE=AUTOCOVER  X1=#R_in*cosd(#alfa_sym) Y1=#R_in*sind(#alfa_sym) Z1=0 EDGETYPE=CENTRE X2=0 Y2=0 Z2=0 
WIREEDGE  CONTINUE=AUTOCOVER  X1=#R_air*cosd(#alfa_sym) Y1=#R_air*sind(#alfa_sym) Z1=0 EDGETYPE=STRAIGHT
WIREEDGE CONTINUE=AUTOCOVER  X1=#R_air Y1=0 Z1=0 EDGETYPE=CENTRE X2=0 Y2=0 Z2=0 

/Sweep Extra Air
PICK OPTION=ADD PROPERTY=UniqueName  LABEL='extra_air'
PICK OPTION=CHANGE TYPE=FACE
SWEEP KEEP=NO TYPE=DISTANCE REGULAR=NO RIGID=NO DISTANCE=#w DRAFTTYPE=NONE

/Extra Air Properties
PICK OPTION=ADD PROPERTY=UniqueName LABEL='extra_air'
CELLDATA OPTION=MODIFY MATERIALLABEL='Air' POTENTIAL=Default ELEMENTTYPE=Linear LEVEL=7 SIZE=5 ELEMSHAPEPREF=NONE

/DRAW Samarium Magnet
/Draw a line from center then choose a point and draw a rectangle from that point.
/Calculate Coordinates
#X1_sam=#k_radius_sam*#R_in*cosd(#k_angle_sam*#alfa_sym)
#Y1_sam=#k_radius_sam*#R_in*sind(#k_angle_sam*#alfa_sym)

#X2_sam=#X1_sam-#L_sam*cosd((#k_angle_sam*#alfa_sym)+#rotation_sam)
#Y2_sam=#Y1_sam-#L_sam*sind((#k_angle_sam*#alfa_sym)+#rotation_sam)

#X3_sam=#X2_sam+#W_sam*sind((#k_angle_sam*#alfa_sym)+#rotation_sam)
#Y3_sam=#Y2_sam-#W_sam*cosd((#k_angle_sam*#alfa_sym)+#rotation_sam)

#X4_sam=#X3_sam+#L_sam*cosd((#k_angle_sam*#alfa_sym)+#rotation_sam)
#Y4_sam=#Y3_sam+#L_sam*sind((#k_angle_sam*#alfa_sym)+#rotation_sam)

SKETCH OBJECT=WIREEDGE +COMPLETE
WIREEDGE  CONTINUE=AUTOCOVER  Name='Sam' X0=#X1_sam Y0=#Y1_sam Z0=0 X1=#X2_sam Y1=#Y2_sam Z1=0 EDGETYPE=STRAIGHT
WIREEDGE  CONTINUE=AUTOCOVER  X1=#X3_sam Y1=#Y3_sam Z1=0 EDGETYPE=STRAIGHT
WIREEDGE  CONTINUE=AUTOCOVER  X1=#X4_sam Y1=#Y4_sam Z1=0 EDGETYPE=STRAIGHT
WIREEDGE  CONTINUE=AUTOCOVER  X1=#X1_sam Y1=#Y1_sam Z1=0 EDGETYPE=STRAIGHT

/Sweep Samarium
PICK OPTION=ADD PROPERTY=UniqueName  LABEL='Sam'
PICK OPTION=CHANGE TYPE=FACE
SWEEP KEEP=NO TYPE=DISTANCE REGULAR=NO RIGID=NO DISTANCE=#w DRAFTTYPE=NONE

/Samarium Properties
PICK OPTION=ADD PROPERTY=UniqueName LABEL='Sam'
CELLDATA OPTION=MODIFY MATERIALLABEL='Samarium' POTENTIAL=Default ELEMENTTYPE=Linear LEVEL=7 SIZE=5 VOLUMELABEL='Sam_orientation' ELEMSHAPEPREF=NONE

/MAGNET ORIENTATIONS
/NdFeB
VOLUME GUIINIT
VOLUME PICK 'Neo_orientation'
VOLUME OPTION=MODIFY THETA=90 PHI=0 PSI=90
/Samarium
VOLUME UNPICK 'Neo_orientation' |VOLUME PICK 'Sam_orientation'
VOLUME OPTION=MODIFY THETA=90 PHI=#Rotation_sam+#k_angle_sam*#alfa_sym-90 PSI=0

/MATERIAL PROPERTIES
MATERIALS GUIINIT
MATERIALS PICK 'NdFeB'
MATERIALS OPTION=MODIFY MULINEARITY=NONLINEAR MUANISOTROPY=ISOTROPIC BH='Neo_BH' 
MATERIALS UNPICK 'NdFeB' | MATERIALS PICK 'Samarium'
MATERIALS OPTION=MODIFY MULINEARITY=NONLINEAR MUANISOTROPY=ISOTROPIC BH='Sam_BH' 
MATERIALS UNPICK 'Samarium' | MATERIALS PICK 'Steel'
MATERIALS OPTION=MODIFY MULINEARITY=NONLINEAR MUANISOTROPY=ISOTROPIC BH='Steel_BH' 

/BH DATA
BHDATA OPTION=EDIT  label=Neo_BH 
BHDATA OPTION=LOAD LABEL=Neo_BH FILE='magnet_neo_bh.bh'
BHDATA OPTION=EDIT  label=Sam_BH 
BHDATA OPTION=LOAD LABEL=Sam_BH FILE=/home/okeysan/Applications/Opera_15/bh/samcob.bh
BHDATA OPTION=EDIT  label=Steel_BH 
BHDATA OPTION=LOAD LABEL=Steel_BH FILE=/home/okeysan/Applications/Opera_15/bh/mildhigh.bh

/TOSCA SETTINGS
ANALYSISDATA OPTION=SET PROGRAM=TOSCAMAGN LINEAR=NO NLITERTYPE=NEWTON NITERATIONS=21 TOLERANCE=1.0E-03 HX=0 HY=0 HZ=0 RHS=ADAPTIVE POTENTIALCUT=YES 

/CREATE MODEL BODY AND MESH
MODEL CREATE

MESH SIZE=4 NORMALTOL=30.0 SURFACETOL=0.0 TOLERANCE=1.0E-03 TYPE=PREFERTETRA 
FILL TOL=1.0E-03 

/CHECK IF THE FILE EXISTS
$ EXIST &filename&.opc
$ IF FILEEXISTS EQ 1
  /SOLVE
  SOLVERS SOLVENOW=NO SAVEMODEL=YES OPTION=NEW FILE='&filename&.op3' UNITS=MM ELEMENT=MIXED SURFACE=CURVED
  YES
$ ELSE
  /SOLVE
  SOLVERS SOLVENOW=NO SAVEMODEL=YES OPTION=NEW FILE='&filename&.op3' UNITS=MM ELEMENT=MIXED SURFACE=CURVED
$ END IF

/SOLVE WITH TOSCA (terminal)
$OS operaanl TOSCA '&filename&.op3' FORE -nodisplay

/OPEN POST-PROCESS FILE (no display)
$OS operapost FORE hybrid_pm_post_process.comi -nodisplay

END
YES
